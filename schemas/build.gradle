group 'com.spring.ws'
version '0.0.1-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

ext {
    schemaDir = "src/main/resources"
    outputDir = "src/main/java"
    schemaFiles = ["${schemaDir}/person.xsd"]
}

repositories {
    mavenCentral()
}

task generate {
    description "Generates Java Sources from XML Schemas"
    group = "build"
    inputs.dir file(schemaDir)
    outputs.dir file(outputDir)
    doLast {
        mkdir outputDir
        javaexec {
            main = 'com.sun.tools.xjc.XJCFacade'
            classpath = configurations.compile
            args '-nv', '-quiet', '-d', outputDir
            args schemaFiles
        }
    }
}

clean {
    delete outputDir
    delete "${schemaDir}/com"
}

compileJava.dependsOn(generate)

jar {
    manifest {
        attributes("Implementation-Title": "persons")
    }
}

dependencies {
    compile "com.sun.xml.bind:jaxb-xjc:2.1.13"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
